/*========================================================================
    Copyright (c) 2012-2016 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using System.Collections;

namespace Cm.Bind
{
    internal void BindTypedef(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, Cm.Core.ClassTemplateRepository& classTemplateRepository,
        Cm.Ast.TypedefNode* typedefNode)
    {
        Cm.Sym.Symbol* symbol = containerScope->Lookup(typedefNode->Id()->Str(), Cm.Sym.SymbolTypeSetId.lookupTypedef);
        if (symbol == null)
        {
            for (const UniquePtr<Cm.Sym.FileScope>& fileScope : fileScopes)
            {
                symbol = fileScope->Lookup(typedefNode->Id()->Str(), Cm.Sym.SymbolTypeSetId.lookupTypedef);
                if (symbol != null) break;
            }
        }
        if (symbol != null)
        {
            if (symbol is Cm.Sym.TypedefSymbol*)
            {
                Cm.Sym.TypedefSymbol* typedefSymbol = cast<Cm.Sym.TypedefSymbol*>(symbol);
                BindTypedef(symbolTable, containerScope, fileScopes, classTemplateRepository, typedefNode, typedefSymbol);
            }
            else
            {
                throw Cm.Core.Error("symbol '" + symbol->FullName() + "' does not denote a typedef", typedefNode->GetSpan(), symbol->GetSpan());
            }
        }
        else
        {
            throw Cm.Core.Error("typedef symbol '" + typedefNode->Id()->Str() + "' not found", typedefNode->GetSpan());
        }
    }
    
    internal void BindTypedef(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, Cm.Core.ClassTemplateRepository& classTemplateRepository,
        Cm.Ast.TypedefStatementNode* typedefStatementNode)
    {
        Cm.Sym.Symbol* symbol = containerScope->Lookup(typedefStatementNode->Id()->Str(), Cm.Sym.SymbolTypeSetId.lookupTypedef);
        if (symbol == null)
        {
            for (const UniquePtr<Cm.Sym.FileScope>& fileScope : fileScopes)
            {
                symbol = fileScope->Lookup(typedefStatementNode->Id()->Str(), Cm.Sym.SymbolTypeSetId.lookupTypedef);
                if (symbol != null) break;
            }
        }
        if (symbol != null)
        {
            if (symbol is Cm.Sym.TypedefSymbol*)
            {
                Cm.Sym.TypedefSymbol* typedefSymbol = cast<Cm.Sym.TypedefSymbol*>(symbol);
                BindTypedef(symbolTable, containerScope, fileScopes, classTemplateRepository, typedefStatementNode, typedefSymbol);
            }
            else
            {
                throw Cm.Core.Error("symbol '" + symbol->FullName() + "' does not denote a typedef", typedefStatementNode->GetSpan(), symbol->GetSpan());
            }
        }
        else
        {
            throw Cm.Core.Error("typedef symbol '" + typedefStatementNode->Id()->Str() + "' not found", typedefStatementNode->GetSpan());
        }
    }
    
    internal void BindTypedef(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, Cm.Core.ClassTemplateRepository& classTemplateRepository,
        Cm.Ast.TypedefNode* typedefNode, Cm.Sym.TypedefSymbol* typedefSymbol)
    {
        if (typedefSymbol->Evaluating())
        {
            throw Cm.Core.Error("cyclic typedef definitions detected", typedefSymbol->GetSpan());
        }
        if (typedefSymbol->IsBound())
        {
            return;
        }
        Cm.Ast.Specifiers specifiers = typedefNode->GetSpecifiers();
        bool isClassMember = typedefNode->Parent() is Cm.Ast.ClassNode*;
        SetAccess(typedefSymbol, specifiers, isClassMember);
        if ((specifiers & Cm.Ast.Specifiers.abstract_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be abstract", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.virtual_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be virtual", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.override_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be override", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.static_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be static", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.explicit_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be explicit", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.external) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be external", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.suppress_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be suppressed", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.default_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be default", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.inline_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be inline", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.constexpr_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be constexpr", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.cdecl_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be cdecl", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.nothrow_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be nothrow", typedefSymbol->GetSpan());
        }
        if ((specifiers & Cm.Ast.Specifiers.throw_) != Cm.Ast.Specifiers.none)
        {
            throw Cm.Core.Error("typedef cannot be throw", typedefSymbol->GetSpan());
        }
        if (typedefSymbol->Parent()->IsClassTemplateSymbol())
        {
            typedefSymbol->SetBound();
            return;
        }
        typedefSymbol->SetEvaluating();
        Cm.Sym.TypeSymbol* type = ResolveType(symbolTable, containerScope, fileScopes, classTemplateRepository, typedefNode->TypeExpr());
        typedefSymbol->ResetEvaluating();
        typedefSymbol->SetType(type);
        typedefSymbol->SetBound();
    }

    internal void BindTypedef(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, Cm.Core.ClassTemplateRepository& classTemplateRepository,
        Cm.Ast.TypedefStatementNode* typedefStatementNode, Cm.Sym.TypedefSymbol* typedefSymbol)
    {
        if (typedefSymbol->Evaluating())
        {
            throw Cm.Core.Error("cyclic typedef definitions detected", typedefSymbol->GetSpan());
        }
        if (typedefSymbol->IsBound())
        {
            return;
        }
        SetAccess(typedefSymbol, Cm.Ast.Specifiers.public_, false);
        typedefSymbol->SetEvaluating();
        Cm.Sym.TypeSymbol* type = ResolveType(symbolTable, containerScope, fileScopes, classTemplateRepository, typedefStatementNode->TypeExpr());
        typedefSymbol->ResetEvaluating();
        typedefSymbol->SetType(type);
        typedefSymbol->SetBound();
    }
}