/*========================================================================
    Copyright (c) 2012-2016 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using System.Collections;

namespace Cm.Bind
{
    internal Cm.Sym.LocalVariableSymbol* BindLocalVariable(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, 
        Cm.Core.ClassTemplateRepository& classTemplateRepository, Cm.Ast.ConstructionStatementNode* constructionStatementNode)
    {
        Cm.Sym.Symbol* symbol = containerScope->Lookup(constructionStatementNode->Id()->Str(), Cm.Sym.SymbolTypeSetId.lookupLocalVariable);
        if (symbol != null)
        {
            if (symbol is Cm.Sym.LocalVariableSymbol*)
            {
                Cm.Sym.LocalVariableSymbol* localVariableSymbol = cast<Cm.Sym.LocalVariableSymbol*>(symbol);
                BindLocalVariable(symbolTable, containerScope, fileScopes, classTemplateRepository, constructionStatementNode, localVariableSymbol);
                return localVariableSymbol;
            }
            else
            {
                throw Cm.Core.Error("symbol '" + symbol->FullName() + "' does not denote a local variable", symbol->GetSpan());
            }
        }
        else
        {
            throw Cm.Core.Error("local variable symbol '" + constructionStatementNode->Id()->Str() + "' not found");
        }
    }
    
    internal void BindLocalVariable(Cm.Sym.SymbolTable& symbolTable, Cm.Sym.ContainerScope* containerScope, const List<UniquePtr<Cm.Sym.FileScope>>& fileScopes, 
        Cm.Core.ClassTemplateRepository& classTemplateRepository, Cm.Ast.ConstructionStatementNode* constructionStatementNode, Cm.Sym.LocalVariableSymbol* localVariableSymbol)
    {
        if (localVariableSymbol->IsBound()) return;
        Cm.Sym.TypeSymbol* type = ResolveType(symbolTable, containerScope, fileScopes, classTemplateRepository, constructionStatementNode->TypeExpr());
        localVariableSymbol->SetType(type);
        localVariableSymbol->SetBound();
    }
}
