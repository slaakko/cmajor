/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using Span = System.Text.Parsing.Span;

namespace Cm.Ast
{
    public class ParameterNode : Node
    {
        public nothrow ParameterNode(const Span& span_) : base(span_)
        {
        }
        public nothrow ParameterNode(const Span& span_, Node* typeExpr_, IdentifierNode* id_) : base(span_), typeExpr(typeExpr_), id(id_)
        {
        }
        public nothrow override NodeType GetNodeType() const
        {
            return NodeType.parameterNode;
        }
        public override Node* Clone(CloneContext& cloneContext) const
        {
            IdentifierNode* clonedId = null;
            if (!id.IsNull())
            {
                clonedId = cast<IdentifierNode*>(id->Clone(cloneContext));
            }
            return new ParameterNode(GetSpan(), typeExpr->Clone(cloneContext), clonedId);
        }
        public nothrow inline Node* TypeExpr() const
        {
            return typeExpr.GetPtr();
        }
        public nothrow inline IdentifierNode* Id() const
        {
            return id.GetPtr();
        }
        public override void Read(Reader& reader)
        {
            typeExpr.Reset(reader.ReadNode());
            typeExpr->SetParent(this);
            bool hasId = reader.GetBinaryReader().ReadBool();
            if (hasId)
            {
                id.Reset(reader.ReadIdentifierNode());
                id->SetParent(this);
            }
        }
        public override void Write(Writer& writer)
        {
            writer.Write(typeExpr.GetPtr());
            bool hasId = !id.IsNull();
            writer.GetBinaryWriter().Write(hasId);
            if (hasId)
            {
                writer.Write(id.GetPtr());
            }
        }
        public override void Accept(Visitor& visitor)
        {
            visitor.Visit(*this);
        }
        public override nothrow string Name() const
        {
            return id->Str();
        }
        public override nothrow string ToString() const
        {
            string s = typeExpr->ToString();
            if (!id.IsNull())
            {
                s.Append(' ').Append(id->ToString());
            }
            return s;
        }
        private UniquePtr<Node> typeExpr;
        private UniquePtr<IdentifierNode> id;
    }
}
