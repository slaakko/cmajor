/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using Span = System.Text.Parsing.Span;

namespace Cm.Ast
{
    public class ConstantNode : Node
    {
        public nothrow ConstantNode(const Span& span_) : base(span_)
        {
        }
        public nothrow ConstantNode(const Span& span_, Specifiers specifiers_, Node* typeExpr_, IdentifierNode* id_, Node* value_) : base(span_), specifiers(specifiers_), typeExpr(typeExpr_), id(id_), value(value_)
        {
            typeExpr->SetParent(this);
            id->SetParent(this);
            value->SetParent(this);
        }
        public nothrow override NodeType GetNodeType() const
        {
            return NodeType.constantNode;
        }
        public nothrow inline Specifiers GetSpecifiers() const
        {
            return specifiers;
        }
        public nothrow inline Node* TypeExpr() const
        {
            return typeExpr.GetPtr();
        }
        public nothrow inline IdentifierNode* Id () const
        {
            return id.GetPtr();
        }
        public nothrow inline Node* Value() const
        {
            return value.GetPtr();
        }
        public override Node* Clone(CloneContext& cloneContext) const
        {
            return new ConstantNode(GetSpan(), specifiers, typeExpr->Clone(cloneContext), cast<IdentifierNode*>(id->Clone(cloneContext)), value->Clone(cloneContext));
        }
        public override void Read(Reader& reader)
        {
            specifiers = reader.ReadSpecifiers();
            typeExpr.Reset(reader.ReadNode());
            typeExpr->SetParent(this);
            id.Reset(reader.ReadIdentifierNode());
            id->SetParent(this);
            value.Reset(reader.ReadNode());
            value->SetParent(this);
        }
        public override void Write(Writer& writer)
        {
            writer.Write(specifiers);
            writer.Write(typeExpr.GetPtr());
            writer.Write(id.GetPtr());
            writer.Write(value.GetPtr());
        }
        public override void Accept(Visitor& visitor)
        {
            visitor.Visit(*this);
        }
        public override nothrow string Name() const
        {
            return id->Str();
        }
        public override nothrow string ToString() const
        {
            return id->Str();
        }
        private Specifiers specifiers;
        private UniquePtr<Node> typeExpr;
        private UniquePtr<IdentifierNode> id;
        private UniquePtr<Node> value;
    }
}
