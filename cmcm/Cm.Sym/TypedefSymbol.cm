/*========================================================================
    Copyright (c) 2012-2016 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using System.Collections;
using Span = System.Text.Parsing.Span;

namespace Cm.Sym
{
    public class TypedefSymbol : Symbol
    {
        public nothrow TypedefSymbol(const Span& span_, const string& name_) : base(span_, name_), type(null), evaluating(false)
        {
        }
        public nothrow override SymbolType GetSymbolType() const
        {
            return SymbolType.typedefSymbol;
        }
        public nothrow override const char* TypeString() const
        {
            return "typedef";
        }
        public nothrow override bool IsExportSymbol() const
        {
            if (Parent()->IsClassTemplateSymbol()) return false;
            if (Parent() is TemplateTypeSymbol*) return false;
            return base->IsExportSymbol();
        }
        public nothrow inline TypeSymbol* GetType() const
        {
            return type;
        }
        public nothrow inline void SetType(TypeSymbol* type_)
        {
            type = type_;
        }
        public nothrow inline bool Evaluating() const
        {
            return evaluating;
        }
        public nothrow inline void SetEvaluating()
        {
            evaluating = true;
        }
        public nothrow inline void ResetEvaluating()
        {
            evaluating = false;
        }
        public override void SetType(TypeSymbol* type_, int index)
        {
            SetType(type_);
        }
        public override void Read(Reader& reader)
        {
            base->Read(reader);
            reader.FetchTypeFor(this, 0);
        }
        public override void Write(Writer& writer)
        {
            base->Write(writer);
            writer.Write(type->Id());
        }
        public override void CollectExportedDerivedTypes(HashSet<Symbol*>& collected, HashSet<TypeSymbol*>& exportedDerivedTypes)
        {
            if (collected.CFind(type) == collected.CEnd())
            {
                collected.Insert(type);
                type->CollectExportedDerivedTypes(collected, exportedDerivedTypes);
            }
        }
        public override void CollectExportedTemplateTypes(HashSet<Symbol*>& collected, HashMap<TypeId, TemplateTypeSymbol*>& exportedTemplateTypes)
        {
            if (collected.CFind(type) == collected.CEnd())
            {
                collected.Insert(type);
                type->CollectExportedTemplateTypes(collected, exportedTemplateTypes);
            }
        }
        public nothrow override void ReplaceReplicaTypes()
        {
            if (type->IsReplica() && type is TemplateTypeSymbol*)
            {
                TemplateTypeSymbol* replica = cast<TemplateTypeSymbol*>(type);
                type = replica->GetPrimaryTemplateTypeSymbol();
            }
        }
        private TypeSymbol* type;
        private bool evaluating;
    }
}
