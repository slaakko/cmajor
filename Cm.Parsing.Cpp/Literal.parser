/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/
 
    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

 ========================================================================*/

[hpp]#include <Cm.Parsing.CppObjectModel/Literal.hpp>

namespace Cm.Parsing.Cpp
{
    [cpp]using namespace Cm::Parsing::CppObjectModel;
    
    grammar LiteralGrammar
    {
        Literal: Cm::Parsing::CppObjectModel::Literal*
                        ::= IntegerLiteral{ value = IntegerLiteral; }
                        |   CharacterLiteral{ value = CharacterLiteral; }
                        |   FloatingLiteral{ value = FloatingLiteral; }
                        |   StringLiteral{ value = StringLiteral; }
                        |   BooleanLiteral{ value = BooleanLiteral; }
                        |   PointerLiteral{ value = PointerLiteral; }
                        ;
                
        IntegerLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= token(DecimalLiteral:dl IntegerSuffix:is1?) { value = new Literal(std::string(matchBegin, matchEnd)); }
                        |   token(OctalLiteral:ol IntegerSuffix:is2?){ value = new Literal(std::string(matchBegin, matchEnd)); }
                        |   token(HexadecimalLiteral:hl IntegerSuffix:is3?){ value = new Literal(std::string(matchBegin, matchEnd)); }
                        ;
                        
        DecimalLiteral  ::= token([1-9] digit*)
                        ;
                        
        OctalLiteral    ::= token('0' OctalDigit*)
                        ;
                        
        OctalDigit      ::= [0-7]
                        ;
                        
        HexadecimalLiteral
                        ::= token(("0x" | "0X")(hexdigit+))
                        ;
                        
        IntegerSuffix   ::= token(UnsignedSuffix (LongLongSuffix | LongSuffix)?)
                        |   token(LongLongSuffix UnsignedSuffix?)
                        |   token(LongSuffix UnsignedSuffix?)
                        ;

        UnsignedSuffix  ::= 'u' | 'U'
                        ;
                        
        LongLongSuffix  ::= "ll" | "LL"
                        ;
                        
        LongSuffix      ::= 'l' | 'L'
                        ;
                        
        CharacterLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= NarrowCharacterLiteral{ value = new Literal(std::string(matchBegin, matchEnd)); }
                        |   UniversalCharacterLiteral{ value = new Literal(std::string(matchBegin, matchEnd)); }
                        |   WideCharacterLiteral{ value = new Literal(std::string(matchBegin, matchEnd)); }
                        ;
                            
        NarrowCharacterLiteral
                        ::= token('\'' CCharSequence '\'')
                        ;
                        
        UniversalCharacterLiteral
                        ::= token('u' '\'' CCharSequence:cs1 '\'')
                        |   token('U' '\'' CCharSequence:cs2 '\'')
                        ;
                        
        WideCharacterLiteral
                        ::= token('L' '\'' CCharSequence '\'')
                        ;
                        
        CCharSequence: std::string
                        ::= token(CChar+){ value = std::string(matchBegin, matchEnd); }
                        ;
                        
        CChar           ::= [^'\\\n]
                        |   EscapeSequence
                        ;
                        
        EscapeSequence  ::= SimpleEscapeSequence
                        |   OctalEscapeSequence
                        |   HexadecimalEscapeSequence
                        ;
                        
        SimpleEscapeSequence
                        ::= "\\'" | "\\\"" | "\\?" | "\\\\" | "\\a" | "\\b" | "\\f" | "\\n" | "\\r" | "\\t" | "\\v"
                        ;
                        
        OctalEscapeSequence
                        ::= '\\' OctalDigit OctalDigit? OctalDigit?
                        ;
                        
        HexadecimalEscapeSequence
                        ::= "\\x" hexdigit+
                        ;
                        
        FloatingLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= token(FractionalConstant ExponentPart? FloatingSuffix?){ value = new Literal(std::string(matchBegin, matchEnd)); }
                        |   token(DigitSequence ExponentPart FloatingSuffix?){ value = new Literal(std::string(matchBegin, matchEnd)); }
                        ;
                        
        FractionalConstant
                        ::= DigitSequence? '.' DigitSequence
                        |   DigitSequence '.'
                        ;
                        
        DigitSequence   ::= digit+
                        ;
        
        ExponentPart    ::= ('e' | 'E') Sign? DigitSequence
                        ;
                        
        Sign            ::= '+' | '-'
                        ;
                        
        FloatingSuffix  ::= [fFlL]
                        ;
                        
        StringLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= token(EncodingPrefix? '"' SCharSequence? '"'){ value = new Literal(std::string(matchBegin, matchEnd)); }
                        ;
                        
        EncodingPrefix  ::= "u8" | 'u' | 'U' | 'L'
                        ;
                        
        SCharSequence   ::= (SChar+)
                        ;
        
        SChar           ::= [^"\\\n]
                        |   EscapeSequence
                        ;
                        
        BooleanLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= keyword("true"){ value = new Literal("true"); }
                        |   keyword("false"){ value = new Literal("false"); }
                        ;
                        
        PointerLiteral: Cm::Parsing::CppObjectModel::Literal*
                        ::= keyword("nullptr"){ value = new Cm::Parsing::CppObjectModel::Literal("nullptr"); }
                        ;
    }
}
