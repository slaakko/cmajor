/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using Cm.Ast;

namespace Cm.Parser
{
    grammar CompileUnitGrammar
    {
        using IdentifierGrammar.Identifier;
        using IdentifierGrammar.QualifiedId;
        using FunctionGrammar.Function;
        using ConstantGrammar.Constant;
        using EnumerationGrammar.EnumType;
        using TypedefGrammar.Typedef;
        using ClassGrammar.Class;
        using DelegateGrammar.Delegate;
        using DelegateGrammar.ClassDelegate;
        using ConceptGrammar.Concept;
        using stdlib.spaces_and_comments;
        skip spaces_and_comments;
        
        CompileUnit(ParsingContext* ctx) : CompileUnitNode*
            ::= empty{ value = new CompileUnitNode(span, fileName); }
                NamespaceContent(ctx, value, value->GlobalNs())
            ;
            
        NamespaceContent(ParsingContext* ctx, CompileUnitNode* compileUnit, NamespaceNode* ns)
            ::= UsingDirectives(ctx, ns)
                Definitions(ctx, compileUnit, ns)
            ;
            
        UsingDirectives(ParsingContext* ctx, NamespaceNode* ns)
            ::= UsingDirective(ctx, ns)*
            ;
            
        UsingDirective(ParsingContext* ctx, NamespaceNode* ns)
            ::= UsingAliasDirective{ ns->AddMember(UsingAliasDirective); }
            |   UsingNamespaceDirective{ ns->AddMember(UsingNamespaceDirective); }
            ;
            
        UsingAliasDirective(var UniquePtr<IdentifierNode> id) : AliasNode*
            ::= (keyword("using") Identifier{ id.Reset(Identifier); } '=' QualifiedId! ';'!){ value = new AliasNode(span, id.Release(), QualifiedId); }
            ;
            
        UsingNamespaceDirective : NamespaceImportNode*
            ::= (keyword("using") QualifiedId! ';'!){ value = new NamespaceImportNode(span, QualifiedId); }
            ;
            
        Definitions(ParsingContext* ctx, CompileUnitNode* compileUnit, NamespaceNode* ns)
            ::= (Definition(ctx, compileUnit, ns){ ns->AddMember(Definition); })*
            ;
            
        Definition(ParsingContext* ctx, CompileUnitNode* compileUnit, NamespaceNode* ns) : Node*
            ::= NamespaceDefinition(ctx, compileUnit, ns){ value = NamespaceDefinition; }
            |   FunctionDefinition(ctx, compileUnit){ value = FunctionDefinition; }
            |   ConstantDefinition(ctx){ value = ConstantDefinition; }
            |   EnumerationDefinition(ctx){ value = EnumerationDefinition; }
            |   TypedefDefinition(ctx){ value = TypedefDefinition; }
            |   ClassDefinition(ctx, compileUnit){ value = ClassDefinition; }
            |   DelegateDefinition(ctx){ value = DelegateDefinition; }
            |   ClassDelegateDefinition(ctx){ value = ClassDelegateDefinition; }
            |   ConceptDefinition(ctx){ value = ConceptDefinition; }
            ;
            
        NamespaceDefinition(ParsingContext* ctx, CompileUnitNode* compileUnit, NamespaceNode* ns): NamespaceNode*
            ::= (keyword("namespace") QualifiedId!){ value = new NamespaceNode(span, QualifiedId); } '{'! NamespaceContent(ctx, compileUnit, value)! '}'!
            ;
            
        FunctionDefinition(ParsingContext* ctx, CompileUnitNode* compileUnit) : FunctionNode*
            ::= Function(ctx){ value = Function; value->SetCompileUnit(compileUnit); }
            ;
            
        ConstantDefinition(ParsingContext* ctx) : Node*
            ::= Constant(ctx){ value = Constant; }
            ;
            
        EnumerationDefinition(ParsingContext* ctx) : Node*
            ::= EnumType(ctx){ value = EnumType; }
            ;
            
        TypedefDefinition(ParsingContext* ctx) : Node*
            ::= Typedef(ctx){ value = Typedef; }
            ;
            
        ClassDefinition(ParsingContext* ctx, CompileUnitNode* compileUnit) : Node*
            ::= Class(ctx, compileUnit){ value = Class; }
            ;
            
        DelegateDefinition(ParsingContext* ctx) : Node*
            ::= Delegate(ctx){ value = Delegate; }
            ;
            
        ClassDelegateDefinition(ParsingContext* ctx) : Node*
            ::= ClassDelegate(ctx){ value = ClassDelegate; }
            ;
            
        ConceptDefinition(ParsingContext* ctx) : Node*
            ::= Concept(ctx){ value = Concept; }
            ;
    }
}
