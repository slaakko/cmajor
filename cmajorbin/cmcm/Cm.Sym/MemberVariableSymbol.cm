/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/

    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

========================================================================*/

using System;
using System.Collections;
using Span = System.Text.Parsing.Span;

namespace Cm.Sym
{
    public class MemberVariableSymbol : Symbol
    {
        public nothrow MemberVariableSymbol(const Span& span_, const string& name_) : base(span_, name_), type(null), layoutIndex(-1)
        {
        }
        public nothrow override SymbolType GetSymbolType() const
        {
            return SymbolType.memberVariableSymbol;
        }
        public nothrow override const char* TypeString() const
        {
            return "member variable";
        }
        public nothrow override bool IsMemberVariableSymbol() const
        {
            return true;
        }
        public nothrow override bool IsExportSymbol() const
        {
            if (Parent()->IsClassTemplateSymbol()) return false;
            if (Parent()->IsTemplateTypeSymbol()) return false;
            return true; 
        }
        public nothrow inline TypeSymbol* GetType() const
        {
            return type;
        }
        public nothrow inline void SetType(TypeSymbol* type_)
        {
            type = type_;
        }
        public override void SetType(TypeSymbol* type_, int index)
        {
            SetType(type_);
        }
        public nothrow inline int LayoutIndex() const
        {
            return layoutIndex;
        }
        public nothrow inline void SetLayoutIndex(int layoutIndex_)
        {
            layoutIndex = layoutIndex_;
        }
        public override void Read(Reader& reader)
        {
            base->Read(reader);
            reader.FetchTypeFor(this, 0);
        }    
        public override void Write(Writer& writer)
        {
            base->Write(writer);
            writer.Write(type->Id());
        }
	 public override void CollectExportedDerivedTypes(HashSet<Symbol*>& collected, HashSet<TypeSymbol*>& exportedDerivedTypes) 
        {
            if (type->IsDerivedTypeSymbol())
            {
                if (collected.CFind(type) == collected.CEnd())
                {
                    collected.Insert(type);
                    type->CollectExportedDerivedTypes(collected, exportedDerivedTypes);
                }
            }
        }
        public override void CollectExportedTemplateTypes(HashSet<Symbol*>& collected, HashMap<TypeId, TemplateTypeSymbol*>& exportedTemplateTypes)
        {
            if (type->IsTemplateTypeSymbol() || type->IsDerivedTypeSymbol())
            {
                if (collected.CFind(type) == collected.CEnd())
                {
                    collected.Insert(type);
                    type->CollectExportedTemplateTypes(collected, exportedTemplateTypes);
                }
            }
        }
        public nothrow override void ReplaceReplicaTypes()
        {
            if (type->IsReplica() && type->IsTemplateTypeSymbol())
            {
                TemplateTypeSymbol* replica = cast<TemplateTypeSymbol*>(type);
                type = replica->GetPrimaryTemplateTypeSymbol();
            }
        }
        private TypeSymbol* type;
        private int layoutIndex;
    }
}
