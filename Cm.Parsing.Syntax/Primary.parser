/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/
 
    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

 ========================================================================*/

[hpp]#include <Cm.Parsing/Parser.hpp>
[hpp]#include <Cm.Parsing/Scope.hpp>
[cpp]#include <Cm.Parsing.Cpp/Expression.hpp>
[cpp]#include <Cm.Parsing.Cpp/Statement.hpp>
[cpp]#include <Cm.Parsing.Syntax/Primitive.hpp>
[cpp]#include <Cm.Parsing.Syntax/Composite.hpp>
[cpp]#include <Cm.Parsing.Syntax/Element.hpp>
[cpp]#include <Cm.Parsing/Nonterminal.hpp>
[cpp]#include <Cm.Parsing.CppObjectModel/Statement.hpp>

namespace Cm.Parsing.Syntax
{
    [cpp]using namespace Cm::Parsing::CppObjectModel;

    grammar PrimaryGrammar
    {
        using PrimitiveGrammar.Primitive;
        using ElementGrammar.Identifier;
        using CompositeGrammar.Alternative;
        using Cm.Parsing.Cpp.ExpressionGrammar.ExpressionList;
        using Cm.Parsing.Cpp.StatementGrammar.CompoundStatement;

        Primary(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                    ::=
                    (   RuleCall{ value = RuleCall; }
                    |   Primitive{ value = Primitive; }
                    |   Grouping(enclosingScope){ value = Grouping; }
                    |   Token(enclosingScope){ value = Token; }
                    )
                    (Expectation(value){ value = Expectation; })?
                    (Action(value){ value = Action; })?
                    ;

        RuleCall: Cm::Parsing::Parser*
                    ::= ((token(Nonterminal:ntWithArgs '(') ExpressionList ')' | Nonterminal:nt) Alias?)
                    {
                        std::string ruleName;
                        if (!ntWithArgs.empty())
                        {
                            ruleName = ntWithArgs;
                        }
                        else
                        {
                            ruleName = nt;
                        }
                        std::string alias = Alias;
                        std::string name = !alias.empty() ? alias : ruleName;
                        NonterminalParser* parser(new NonterminalParser(name, ruleName));
                        parser->SetSpan(span);
                        if (!ExpressionList.empty())
                        {
                            parser->SetArguments(ExpressionList); 
                        }
                        value = parser;
                    }
                    ;

        Nonterminal: std::string 
                    ::= Identifier{ value = Identifier; }
                    ;

        Alias: std::string
                    ::= ':' Identifier!{ value = Identifier; }
                    ;

        Grouping(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                    ::= '(' Alternative(enclosingScope)! ')'!{ value = Alternative; }
                    ;

        Token(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                    ::= keyword("token") '('! Alternative(enclosingScope)! ')'!{ value = new TokenParser(Alternative); }
                    ;

        Expectation(Cm::Parsing::Parser* child): Cm::Parsing::Parser* 
                    ::= '!'{ value = new ExpectationParser(child); }
                    ;

        Action(Cm::Parsing::Parser* child): Cm::Parsing::Parser*
                    ::= (CompoundStatement:action ('/' CompoundStatement:failureAction)?)
                    {
                        value = new ActionParser("", action, failureAction, child);
                    }
                    ;
    }
}
