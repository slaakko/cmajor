/*========================================================================
    Copyright (c) 2012-2016 Seppo Laakko
    http://sourceforge.net/projects/cmajor/
 
    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

 ========================================================================*/

[hpp]#include <Cm.Parsing/Scope.hpp>
[hpp]#include <Cm.Parsing/Parser.hpp>
[cpp]#include <Cm.Parsing/Composite.hpp>
[cpp]#include <Cm.Parsing.Syntax/Primary.hpp>

namespace Cm.Parsing.Syntax
{
    grammar CompositeGrammar
    {
        using PrimaryGrammar.Primary;

        Alternative(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= Sequence(enclosingScope):left{ value = left; } ('|' Sequence(enclosingScope):right!{ value = new AlternativeParser(value, right); })*
                        ;

        Sequence(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= Difference(enclosingScope):left{ value = left; } (Difference(enclosingScope):right{ value = new SequenceParser(value, right); })*
                        ;

        Difference(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= ExclusiveOr(enclosingScope):left{ value = left; } ('-' ExclusiveOr(enclosingScope):right!{ value = new DifferenceParser(value, right); })*
                        ;

        ExclusiveOr(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= Intersection(enclosingScope):left{ value = left; } ('^' Intersection(enclosingScope):right!{ value = new ExclusiveOrParser(value, right); })*
                        ;

        Intersection(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= List(enclosingScope):left{ value = left; } ('&' List(enclosingScope):right{ value = new IntersectionParser(value, right); })*
                        ;

        List(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser* 
                        ::= Postfix(enclosingScope):left{ value = left; } ('%' Postfix(enclosingScope):right{ value = new ListParser(value, right); })?
                        ;

        Postfix(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Parser*
                        ::= Primary(enclosingScope){ value = Primary; }
                        (   '*'{ value = new KleeneStarParser(value); }
                        |   '+'{ value = new PositiveParser(value); }
                        |   '?'{ value = new OptionalParser(value); }
                        )?
                        ;
    }
}
