/*========================================================================
    Copyright (c) 2012-2015 Seppo Laakko
    http://sourceforge.net/projects/cmajor/
 
    Distributed under the GNU General Public License, version 3 (GPLv3).
    (See accompanying LICENSE.txt or http://www.gnu.org/licenses/gpl.html)

 ========================================================================*/

[hpp]#include <Cm.Parsing/Grammar.hpp>
[cpp]#include <Cm.Parsing.Syntax/Element.hpp>
[cpp]#include <Cm.Parsing.Syntax/Rule.hpp>

namespace Cm.Parsing.Syntax
{
    grammar GrammarGrammar
    {
        using ElementGrammar.Identifier;
        using ElementGrammar.QualifiedId;
        using ElementGrammar.Signature;
        using ElementGrammar.RuleLink;
        using RuleGrammar.Rule;

        Grammar(Cm::Parsing::Scope* enclosingScope): Cm::Parsing::Grammar*
                    ::= keyword("grammar") Identifier:name!{ value = new Grammar(name, enclosingScope); value->SetSpan(span); value->SetNs(enclosingScope->Ns()); } 
                        '{'! GrammarContent(value) '}'!
                    ;
                    
        GrammarContent(Cm::Parsing::Grammar* grammar)
                    ::= 
                    (   StartClause(grammar)
                    |   SkipClause(grammar)
                    |   RuleLink(grammar)
                    |   Rule(grammar->GetScope()){ grammar->AddRule(Rule); }
                    )*
                    ;

        StartClause(Cm::Parsing::Grammar* grammar)
                    ::= keyword("start") Identifier:strt! ';'{ grammar->SetStartRuleName(strt); }
                    ;

        SkipClause(Cm::Parsing::Grammar* grammar) 
                    ::= keyword("skip") QualifiedId:skp! ';'{ grammar->SetSkipRuleName(skp); }
                    ;
    }
}
